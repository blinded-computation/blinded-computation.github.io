
<!DOCTYPE html PUBLIC "-//W3C//DTD XHTML 1.0 Transitional//EN"
"http://www.w3c.org/TR/xhtml1/DTD/xhtml1-transitional.dtd">



<html xmlns="http://www.w3.org/1999/xhtml">
    <head>
        <meta http-equiv="Content-Type" content="text/html; charset=utf-8" />
        <meta http-equiv="content-style-type" content="text/css">
        <meta http-equiv="content-script-type" content="text/javascript">
        <meta name=generator content="Sphinx">
        <meta name=viewport content="width=device-width,initial-scale=1.0">
        <meta name="generator" content="Docutils 0.17.1: http://docutils.sourceforge.net/" />

        <title>Model ISA &mdash; BliME formal model  documentation</title>

        
    <link rel="stylesheet" href="_static/pygments.css" type="text/css" />
    <link rel="stylesheet" href="_static/main.css" type="text/css" />
    <link rel="stylesheet" href="_static/genericons.css" type="text/css" /><link rel="stylesheet" href="https://cdnjs.cloudflare.com/ajax/libs/codemirror/5.36.0/codemirror.min.css" type="text/css" /><link rel="stylesheet" href="_static/cm.tango.css" type="text/css" /><link rel="stylesheet" href="_static/fslit.css" type="text/css" />
    <link rel="stylesheet" href="_static/mobile.css" type="text/css" media="only screen and (max-device-width: 568px)">
        
    <script type="text/javascript">
      var DOCUMENTATION_OPTIONS = {
        URL_ROOT:    './',
        VERSION:     '',
        COLLAPSE_INDEX: false,
        FILE_SUFFIX: '.html',
        HAS_SOURCE:  true
      };
    </script>
    <script type="text/javascript" src="_static/jquery.js"></script>
    <script type="text/javascript" src="_static/underscore.js"></script>
    <script type="text/javascript" src="_static/doctools.js"></script>
    <script type="text/javascript" src="_static/language_data.js"></script>
    <script type="text/javascript" src="https://cdnjs.cloudflare.com/ajax/libs/codemirror/5.36.0/codemirror.min.js"></script>
    <script type="text/javascript" src="_static/fstar.cm.js"></script>
    <script type="text/javascript" src="_static/fslit.js"></script>
    <script type="text/javascript" src="https://cdnjs.cloudflare.com/ajax/libs/mathjax/2.7.5/latest.js?config=TeX-AMS-MML_HTMLorMML"></script>
    <script type="text/javascript" src="_static/backtop.js"></script>

    </head>
    <body>
    <div class="content">
        <header id="top-header">
            <div class="left">
    <ul class="breadcrumb">
        <li>
            <a href="index.html">BliME formal model  documentation</a>
             &raquo;
        </li> 
    </ul>
            </div>
            <div class="right">
                
    <ul class="relative">
      <li >
            <a href="genindex.html" title="General Index"
               class="icon-index"
                accesskey="I"></a>
        </li>
      <li >
            <a href="Value.html" title="Blindable values"
               class="icon-next"
                accesskey="N"></a>
        </li>
      <li >
            <a href="InstructionDecoder.html" title="Load-store model"
               class="icon-previous"
                accesskey="P"></a>
        </li>
    </ul>
            </div>
            <div class="clearer"></div>
        </header>
        

        <div class="documentwrapper">
        <div class="sidebarwrapper">
            <div class="sidebar">
                    <h3><a href="index.html">Top</a></h3><nav class="localtoc">
      <ul>
<li><a class="reference internal" href="#">Model ISA</a><ul>
<li><a class="reference internal" href="#instruction-format">Instruction format</a></li>
<li><a class="reference internal" href="#instruction-decoder">Instruction decoder</a><ul>
<li><a class="reference internal" href="#utility-functions">Utility functions</a></li>
</ul>
</li>
<li><a class="reference internal" href="#instruction-semantics">Instruction semantics</a><ul>
<li><a class="reference internal" href="#safety">Safety</a></li>
<li><a class="reference internal" href="#correctness">Correctness</a></li>
</ul>
</li>
</ul>
</li>
</ul>

</nav><div class="relative">
      <h4>Previous topic</h4>
      <p class="topless"><a href="InstructionDecoder.html"
                            title="previous chapter">Load-store model</a></p>
      <h4>Next topic</h4>
      <p class="topless"><a href="Value.html"
                            title="next chapter">Blindable values</a></p>
</div>
  <h4>This Page</h4>
  <a href="_sources/ISA.fst.txt"
           rel="nofollow">Show Source</a>
<div id="searchbox" style="display: none">
  <h5>Quick search</h5>
    <form class="search" action="search.html" method="get">
      <input type="text" name="q" placeholder=' quick search' />
      <input type="hidden" name="check_keywords" value="yes" />
      <input type="hidden" name="area" value="default" />
    </form>
</div>
<script type="text/javascript">$('#searchbox').show(0);</script>
            </div>
        </div>
            <div class="document">
                <div class="body">
                    
    <section id="model-isa">
<h1>Model ISA<a class="headerlink" href="#model-isa" title="Permalink to this headline">¶</a></h1>
<p>In this file, we analyze a concrete instruction set that includes a number
of manually-implemented taint propagation rules.</p>
<pre class="code fstar literal-block"><code>module ISA

open FStar.Math.Lib
open FStar.Mul

open Cpu
open Memory
open InstructionDecoder
open Value</code></pre>
<section id="instruction-format">
<h2>Instruction format<a class="headerlink" href="#instruction-format" title="Permalink to this headline">¶</a></h2>
<p>We start by defining the instruction format.</p>
<table class="docutils align-default">
<colgroup>
<col style="width: 22%" />
<col style="width: 6%" />
<col style="width: 8%" />
<col style="width: 8%" />
<col style="width: 8%" />
<col style="width: 8%" />
<col style="width: 8%" />
<col style="width: 8%" />
<col style="width: 8%" />
<col style="width: 8%" />
<col style="width: 6%" />
</colgroup>
<tbody>
<tr class="row-odd"><td><p><strong>Field</strong></p></td>
<td colspan="2"><p>opcode</p></td>
<td colspan="2"><p>out1</p></td>
<td colspan="2"><p>in1</p></td>
<td colspan="2"><p>in2</p></td>
<td colspan="2"><p>imm</p></td>
</tr>
<tr class="row-even"><td><p><strong>Bits</strong></p></td>
<td><p>63</p></td>
<td><p>61</p></td>
<td><p>60</p></td>
<td><p>56</p></td>
<td><p>55</p></td>
<td><p>51</p></td>
<td><p>50</p></td>
<td><p>46</p></td>
<td><p>45</p></td>
<td><p>0</p></td>
</tr>
</tbody>
</table>
<p>Given this format, we then define functions to extract each component of the
instruction word.</p>
<p>First, we define a conversion function mapping a machine word to an integer.</p>
<pre class="code fstar literal-block"><code>let v = FStar.UInt64.v</code></pre>
<p>Then we define functions that extract the various components</p>
<pre class="code fstar literal-block"><code>let parse_opcode (inst:word) = (arithmetic_shift_right (v inst) 61) % 8
let parse_out1 (inst:word): nat = (arithmetic_shift_right (v inst) 56) % 32
let parse_in1 (inst:word): nat = (arithmetic_shift_right (v inst) 51) % 32
let parse_in2 (inst:word): nat = (arithmetic_shift_right (v inst) 45) % 32</code></pre>
<p>However, for immediate values it is more convenient to define the function
to extract a machine word rather than an integer.</p>
<pre class="code fstar literal-block"><code>let parse_imm (inst:word): Cpu.word =
      (FStar.UInt64.logand inst (FStar.UInt64.sub (FStar.UInt64.shift_left 1uL 46ul) 1uL))</code></pre>
</section>
<section id="instruction-decoder">
<h2>Instruction decoder<a class="headerlink" href="#instruction-decoder" title="Permalink to this headline">¶</a></h2>
<p>Next we set up the instruction decoder, which converts an instruction word
into an opcode plus a set of operands.</p>
<pre class="code fstar literal-block"><code>let sample_decoder (inst:word): decodedInstruction =
  let opcode = parse_opcode inst in
  let out1 = parse_out1 inst in
  let in1 = parse_in1 inst in
  let in2 = parse_in2 inst in
  let imm = parse_imm inst in
  match opcode with
  (* Store *)
    | 0 -&gt; { opcode;
            input_operands  = [ Register in1; Register in2 ];
            output_operands = [] }
  (* Load *)
    | 1 -&gt; { opcode;
            input_operands  = [ Register in1; Register in2 ];
            output_operands = [] }
  (* Branch *)
    | 2 -&gt; { opcode;
            input_operands  = [PC; Register in1; Register in2];
            output_operands = [PC] }
  (* Add *)
    | 3 -&gt; { opcode;
            input_operands  = [ Register in1; Register in2; Immediate imm ];
            output_operands = [ Register out1 ] }
  (* Subtract *)
    | 4 -&gt; { opcode;
            input_operands  = [ Register in1; Register in2 ];
            output_operands = [ Register out1 ] }
  (* Multiply *)
    | 5 -&gt; { opcode;
            input_operands  = [ Register in1; Register in2 ];
            output_operands = [ Register out1 ] }
  (* Bitwise AND *)
    | 6 -&gt; { opcode;
            input_operands  = [ Register in1; Register in2 ];
            output_operands = [ Register out1 ] }
  (* Bitwise XOR *)
    | 7 -&gt; { opcode;
            input_operands  = [ Register in1; Register in2 ];
            output_operands = [ Register out1 ] }</code></pre>
<p>Later on we will need to know how many input operands each instruction has;
the prover can work it out, but needs a hint.</p>
<pre class="code fstar literal-block"><code>let sample_decoded_instruction_length (inst:word):
  Lemma (ensures FStar.List.length (sample_decoder inst).input_operands =
                    (match (sample_decoder inst).opcode with
                     | 0 -&gt; 2
                     | 1 -&gt; 2
                     | 2 -&gt; 3
                     | 3 -&gt; 3
                     | 4 -&gt; 2
                     | 5 -&gt; 2
                     | 6 -&gt; 2
                     | 7 -&gt; 2
                     )
        )
        [SMTPat (sample_decoder inst)]=
  ()
</code></pre>
<section id="utility-functions">
<h3>Utility functions<a class="headerlink" href="#utility-functions" title="Permalink to this headline">¶</a></h3>
<pre class="code fstar literal-block"><code>let rec any_value_is_blinded (values: list (maybeBlinded #word)): bool =
  match values with
    | Nil              -&gt; false
    | Blinded(hd) :: _  -&gt; true
    | Clear(hd)   :: tl -&gt; any_value_is_blinded tl

let rec equivalent_lists_have_identical_any_blindedness (a b: list (maybeBlinded #word)):
  Lemma (requires (equiv_list a b))
        (ensures (any_value_is_blinded a) = (any_value_is_blinded b))
  = match a, b with
      | hl::tl, hr::tr -&gt; equivalent_lists_have_identical_any_blindedness tl tr
      | _ -&gt; ()

let rec blind_all_values (values: list (maybeBlinded #word)): r:(list (maybeBlinded #word)){FStar.List.length values = FStar.List.length r} =
    match values with
      | Nil              -&gt; Nil
      | Blinded(hd) :: tl -&gt; Blinded(hd) :: blind_all_values tl
      | Clear(hd)   :: tl -&gt; Blinded(hd) :: blind_all_values tl

let rec blinding_all_values_blinds_each_value (values: list (maybeBlinded #word)) (n:nat{n &lt; FStar.List.length values}):
  Lemma (ensures Blinded? (FStar.List.Tot.index (blind_all_values values) n))
        [SMTPat (Blinded? (FStar.List.Tot.index (blind_all_values values) n))] =
  match n, values with
    | 0, _   -&gt; ()
    | _, Nil -&gt; ()
    | n, hd :: tl -&gt; blinding_all_values_blinds_each_value tl (n-1)


let rec blinding_all_values_is_idempotent (values: list (maybeBlinded #word)):
  Lemma (ensures (blind_all_values values) = blind_all_values (blind_all_values values)) =
    match values with
      | hd :: tl -&gt; blinding_all_values_is_idempotent tl
      | _ -&gt; ()

let rec blinding_all_values_blinds_all_values (values: list (maybeBlinded #word)):
  Lemma (ensures all_values_are_blinded _ (blind_all_values values))
        [SMTPat (blind_all_values values)] =
  match values with
    | hd :: tl -&gt; blinding_all_values_blinds_all_values tl
    | _ -&gt; ()

let rec equivalent_unblinded_lists_are_equal (a b: list (maybeBlinded #word)):
  Lemma (requires (equiv_list a b) /\ (~(any_value_is_blinded a) \/ ~(any_value_is_blinded b)))
        (ensures a = b) =
  match a, b with
    | h1 :: t1, h2 :: t2 -&gt; equivalent_unblinded_lists_are_equal t1 t2
    | _ -&gt; ()
</code></pre>
</section>
</section>
<section id="instruction-semantics">
<h2>Instruction semantics<a class="headerlink" href="#instruction-semantics" title="Permalink to this headline">¶</a></h2>
<p>Now we define the behavior of each instruction.</p>
<p>In most cases we use a ‘generic’ taint propagation rule:</p>
<p>However, there are some instructions where a more specific rule is
appropriate.  For example, <span class="math notranslate nohighlight">\(x \oplus x\)</span> and <span class="math notranslate nohighlight">\(x \&amp; 0\)</span> both
evaluate to zero, irrespective of the value of <span class="math notranslate nohighlight">\(x\)</span>, meaning that
the result can be <code class="docutils literal notranslate"><span class="pre">Clear</span> <span class="pre">0</span></code> even though an input may be blinded.</p>
<pre class="code fstar literal-block"><code>val sample_semantics (#n:memory_size): instruction_semantics #n #32 sample_decoder

#set-options &quot;--z3rlimit 1000&quot;
let sample_semantics (di:decoder_output sample_decoder)
                     (pre:(list (maybeBlinded #word)){
                       (exists(s: systemState). pre = get_operands di.input_operands s) /\
                       FStar.List.length pre = FStar.List.length di.input_operands})
                     : instruction_result di =
  match di.opcode with
    (* Store *)
    | 0 -&gt; let address = (FStar.List.Tot.index pre 0) in

    { register_writes = []; memory_ops = if Blinded? address then [] else (
      assert(FStar.List.length di.input_operands = 2);
      match (FStar.List.Tot.index di.input_operands 0), (FStar.List.Tot.index di.input_operands 1) with
        | Register d, Register s -&gt; [Store (v (unwrap address)) s]
        | _ -&gt; []
      )}
    (* Load *)
    | 1 -&gt; let address = (FStar.List.Tot.index pre 0) in
          { register_writes = [];
            memory_ops = if Blinded? address
                         then []
                         else match (FStar.List.Tot.index di.input_operands 0),
                                    (FStar.List.Tot.index di.input_operands 1) with
                               | Register d, Register s -&gt; [Load (v (unwrap address)) d]
                               | _ -&gt; []}
    (* Branch *)
    | 2 -&gt; ( let pc = FStar.List.Tot.index pre 0 in
            let a = FStar.List.Tot.index pre 1 in
            let b = FStar.List.Tot.index pre 2 in
            let ref: maybeBlinded #word = Clear #word 0uL in
            { register_writes = [if Blinded? a then Clear 0uL else if a = ref then b else pc];
              memory_ops = [] })
    (* Add *)
    | 3 -&gt; ( assert(FStar.List.length pre = 3);
            let a: maybeBlinded #Cpu.word = FStar.List.Tot.index pre 0 in
            let b: maybeBlinded #Cpu.word = FStar.List.Tot.index pre 1 in
            let result: Cpu.word = (FStar.UInt64.add_mod (unwrap a) (unwrap b)) in
            let result = if any_value_is_blinded pre then Blinded result else Clear result in
            { register_writes = [result];
              memory_ops = [] })
    (* Sub *)
    | 4 -&gt; ( assert(FStar.List.length pre = 2);
            let a = FStar.List.Tot.index pre 0 in
            let b = FStar.List.Tot.index pre 1 in
            let result: Cpu.word = (FStar.UInt64.sub_mod (unwrap a) (unwrap b)) in
            let result = if any_value_is_blinded pre then Blinded result else Clear result in
            { register_writes = [result];
              memory_ops = [] })
    (* MUL *)
    | 5 -&gt; ( assert(FStar.List.length pre = 2);
            let a = FStar.List.Tot.index pre 0 in
            let b = FStar.List.Tot.index pre 1 in
            let result = (FStar.UInt64.mul_mod (unwrap a) (unwrap b)) in
            let result = if any_value_is_blinded pre then
                            Blinded result
                         else Clear result in
           { register_writes = [result];
             memory_ops = [] })
    (* AND *)
    | 6 -&gt; ( assert(FStar.List.length pre = 2);
            let a: maybeBlinded #word = FStar.List.Tot.index pre 0 in
            let b: maybeBlinded #word = FStar.List.Tot.index pre 1 in
            let result: Cpu.word = FStar.UInt64.logand (unwrap a) (unwrap b) in
            let result = if (a = Clear #word 0uL) || (b = Clear #word 0uL) then
                            Clear 0uL
                         else if any_value_is_blinded pre then
                            Blinded result
                         else Clear result in
                         { register_writes = [result];
                           memory_ops = [];}
          )
    (* XOR *)
    | 7 -&gt; ( assert(FStar.List.length pre = 2);
            let a = FStar.List.Tot.index pre 0 in
            let b = FStar.List.Tot.index pre 1 in
            let result: Cpu.word = (FStar.UInt64.logxor (unwrap a) (unwrap b)) in
            let result = if (FStar.List.Tot.index di.input_operands 0)
                           = (FStar.List.Tot.index di.input_operands 1) then
                            Clear 0uL
                         else if any_value_is_blinded pre then
                            Blinded result
                         else Clear result in
                         { register_writes = [result];
                           memory_ops = [];}
          )

irreducible let trigger (#n: memory_size)
  (inst:word) (pre:instruction_input #n #32 (sample_decoder inst)) = False</code></pre>
<section id="safety">
<h3>Safety<a class="headerlink" href="#safety" title="Permalink to this headline">¶</a></h3>
<p>Now we can start to prove the safety of the instruction semantics, which we
do by proving their redaction-equivalence.</p>
<p>This is complicated slightly by the fact that we need to show that the
redacted inputs are also valid inputs.</p>
<p>We start by using an special redacting function on the instruction inputs
that shows that the result is also an input to said instruction.</p>
<pre class="code fstar literal-block"><code>#set-options &quot;--z3rlimit 1000&quot;
let sample_semantics_are_redacting_equivalent_expanded (#n: memory_size)
   (inst:word)
   (pre:instruction_input (sample_decoder inst)):
  Lemma (ensures (equiv_result (sample_semantics #n (sample_decoder inst) pre)
                               (sample_semantics #n (sample_decoder inst)
                                  (redacted_instruction_inputs_are_instruction_inputs #n #32
                                     (sample_decoder inst) pre)))
                 \/ trigger #n inst pre) =
  ()</code></pre>
<p>Then we do the real proof of redacting-equivalence.</p>
<pre class="code fstar literal-block"><code>#set-options &quot;--z3rlimit 10000&quot;
let sample_semantics_are_redacting_equivalent (#n: memory_size)
    (inst:word)
    (pre:instruction_input #n #32 (sample_decoder inst)):
  Lemma (ensures is_redacting_equivalent_instruction_semantics_somewhere #n #32
                                                                         sample_decoder
                                                                         sample_semantics
                                                                         inst pre
                 \/ trigger inst pre) =
  ()

let sample_semantics_are_redacting_equivalent_everywhere (#n: memory_size):
  Lemma (ensures is_redacting_equivalent_instruction_semantics_everywhere #n #32
                   sample_decoder sample_semantics) =
  ()</code></pre>
<p>Finally, we show that the semantics are safe.</p>
<pre class="code fstar literal-block"><code>let sample_semantics_are_safe (#n: memory_size) (cp:cache_policy n):
  Lemma (ensures is_safe #n #32
                         (loadstore_execution_unit #n #32 sample_decoder (sample_semantics #n) cp)) =
    sample_semantics_are_redacting_equivalent_everywhere #n;

    loadstore_execution_unit_with_re_instruction_semantics_is_redacting_equivalent
      sample_decoder (sample_semantics #n) cp;

    each_loadstore_execution_unit_with_redacting_equivalent_instruction_semantics_is_safe
      sample_decoder (sample_semantics #n) cp
</code></pre>
</section>
<section id="correctness">
<h3>Correctness<a class="headerlink" href="#correctness" title="Permalink to this headline">¶</a></h3>
<p>Next we prove that the instructions do the right thing.  First, addition:</p>
<pre class="code fstar literal-block"><code>let add_instruction_works_correctly
  (#n:memory_size) (inst:word)
  (pre:(list (maybeBlinded #word)){
    (exists(s: systemState #n).
      pre = get_operands #n #32 (sample_decoder inst).input_operands s) /\
      FStar.List.length pre = FStar.List.length (sample_decoder inst).input_operands}):

    Lemma (requires parse_opcode inst = 3)
          (ensures  unwrap (FStar.List.Tot.hd
                             (sample_semantics #n (sample_decoder inst) pre).register_writes)
                     = (FStar.UInt64.add_mod (unwrap (FStar.List.Tot.index pre 0))
                                             (unwrap (FStar.List.Tot.index pre 1)))) =
    ()
</code></pre>
<p>Then exclusive or.  First we need to know that <span class="math notranslate nohighlight">\(x \oplus x = 0\)</span>, since this means
that the output won’t depend on the specific value of <span class="math notranslate nohighlight">\(x\)</span>.</p>
<pre class="code fstar literal-block"><code>let xor_with_self_yields_zero (a:Cpu.word):
  Lemma (ensures (FStar.UInt64.logxor a a) = 0uL) =
    let value: FStar.UInt.uint_t 64 = v a in
    FStar.UInt.logxor_self #64 value;
    assert(FStar.UInt.logxor #64 value value == 0)</code></pre>
<p>Then we show that the computed value really is the exclusive OR of the inputs.</p>
<pre class="code fstar literal-block"><code>let xor_instruction_works_correctly
      (#n:memory_size) (inst:word)
      (pre:(list (maybeBlinded #word)){
        (exists(s: systemState #n).
          pre = get_operands #n #32 (sample_decoder inst).input_operands s) /\
          FStar.List.length pre = FStar.List.length (sample_decoder inst).input_operands}):
    Lemma (requires parse_opcode inst = 7)
          (ensures  unwrap (FStar.List.Tot.hd
                             (sample_semantics #n (sample_decoder inst) pre).register_writes)
                     = FStar.UInt64.logxor (unwrap (FStar.List.Tot.index pre 0))
                                           (unwrap (FStar.List.Tot.index pre 1)) ) =
          let di = sample_decoder inst in
          let a = FStar.List.Tot.index pre 0 in
          let b = FStar.List.Tot.index pre 1 in
          let outputs = (sample_semantics #n (sample_decoder inst) pre).register_writes in
          if (FStar.List.Tot.index di.input_operands 0)
              = (FStar.List.Tot.index di.input_operands 1) then (
            assert(a == b);
            xor_with_self_yields_zero (unwrap a) )
          else if any_value_is_blinded pre then
            assert(outputs = [Blinded (FStar.UInt64.logxor (unwrap a) (unwrap b))])
          else
            assert(outputs = [Clear (FStar.UInt64.logxor (unwrap a) (unwrap b))])</code></pre>
</section>
</section>
</section>

    <div id="raise-top"></div>

                </div>
            </div> <!-- document -->


        </div> <!-- documentwrapper -->
        <footer>
            <p class="show-copyright">
                &copy; Copyright 2022.
            </p>
            <p class="show-sphinx">
              Created using <a href="http://sphinx.pocoo.org/">Sphinx</a> 2.4.3.
            </p>
            
              <p class="theme-info"><a href="https://pypi.python.org/pypi/hachibee-sphinx-theme">Sphinx-hachibee-theme</a> is hosted on <a href="https://github.com/hachibeeDI/Sphinx-hachibee-theme">Github</a></p>
            
        </footer>
    </div>
    <script type="text/javascript" src="_static/main.js" async></script>
    </body>
</html>
